Перем Данные;
Перем СчетчикБлоков;

#Область ПрограммныйИнтерфейс

Функция ПолучитьСледующийБлокДанных(ВыходныеДанные) Экспорт
	
	Если СчетчикБлоков >= КоличествоБлоков Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РазмерШага = (ДатаОкончания - ДатаНачала) / КоличествоБлоков;
	
	ДатаН = ДатаНачала + СчетчикБлоков * РазмерШага;
	СчетчикБлоков = СчетчикБлоков + 1;
	ДатаК = ДатаНачала + СчетчикБлоков * РазмерШага;
	
	Если КоличествоБлоков = СчетчикБлоков Тогда
		ДатаК = ДатаОкончания;
	КонецЕсли;
	
	Выборка = ПолучитьДанныеИзБазы(ДатаН, ДатаК);
	ВыходныеДанные = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		Структура = Новый Структура();
		Структура.Вставить("Регистратор");
		Структура.Вставить("Период");
		Структура.Вставить("Город");
		Структура.Вставить("Товар");
		Структура.Вставить("Количество");
		Структура.Вставить("Цена");
		Структура.Вставить("Сумма");
		ЗаполнитьЗначенияСвойств(Структура, Выборка);
		ВыходныеДанные.Добавить(Структура);
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция ИмяМетодаMAP() Экспорт 
	Возврат "MapReduce.Map";
КонецФункции

Функция ИмяМетодаShuffle() Экспорт 
	Возврат "MapReduce.Shuffle";
КонецФункции

Функция ИмяМетодаReduce() Экспорт 
	Возврат "MapReduce.Reduce";
КонецФункции


#КонецОбласти

#Область Служебные

Функция ПолучитьДанныеИзБазы(ДатаН, ДатаК)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	мрДанные.Регистратор КАК Регистратор,
	|	мрДанные.Период КАК Период,
	|	мрДанные.Город КАК Город,
	|	мрДанные.Товар КАК Товар,
	|	мрДанные.Количество КАК Количество,
	|	мрДанные.Цена КАК Цена,
	|	мрДанные.Сумма КАК Сумма
	|ИЗ
	|	РегистрСведений.мрДанные КАК мрДанные
	|ГДЕ
	|	мрДанные.Период МЕЖДУ &ДатаНачала И &ДатаОкончания";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаН);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаК);
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выбрать();
		
КонецФункции

#КонецОбласти

СчетчикБлоков = 0;
